\begin{comment}
\begin{code}
{-# OPTIONS --rewriting #-}

module cpp2024.STLC where

open import Data.List using (List; []; _âˆ·_; length)
open import Data.Nat
open import Data.Nat.Induction
open import Data.Bool using (true; false) renaming (Bool to ğ”¹)
open import Data.List using (map)
open import Data.Nat.Properties
open import Data.Product using (_,_;_Ã—_; projâ‚; projâ‚‚; Î£-syntax; âˆƒ-syntax)
--open import Data.Unit.Polymorphic using (âŠ¤; tt)
open import Data.Unit using (âŠ¤; tt)
open import Data.Unit.Polymorphic renaming (âŠ¤ to topáµ–; tt to ttáµ–)
open import Data.Vec using (Vec) renaming ([] to []ÌŒ; _âˆ·_ to _âˆ·ÌŒ_)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Induction using (RecStruct)
open import Induction.WellFounded as WF
open import Data.Product.Relation.Binary.Lex.Strict
  using (Ã—-Lex; Ã—-wellFounded; Ã—-preorder)
open import Relation.Binary using (Rel)
open import Relation.Binary.PropositionalEquality as Eq
  using (_â‰¡_; _â‰¢_; refl; sym; cong; congâ‚‚; subst; trans)
--open Eq.â‰¡-Reasoning
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Sig
open import Var
open import cpp2024.StepIndexedLogic

\end{code}
\end{comment}

\section{Case Study: Type Safety of the STLC with Recursive Functions}

We provide an example application of our Step-Indexed Logic with a
case study in proving semantic type safety for the STLC with recursive
functions. We choose to extend STLC with recursive functions because
otherwise, one does not need step-indexed logical relations; logical
relations that are only indexed by types are sufficient.  The next few
subsections give the definition of this variant of the STLC (syntax in
Â§\ref{sec:STLC-syntax}, reduction semantics in
Â§\ref{sec:STLC-reduction}, type system in
Â§\ref{sec:STLC-type-system}). We then define the step-indexed logical
relation in Section~\ref{sec:log-rel} and use it to define semantic
type safety in Section~\ref{sec:sem-type-safety}. The rest of the
subsections give the proof of semantic type safety, starting
with the Bind Lemma (Â§\ref{sec:bind-lemma}), then the many
Compatibility Lemmas (Â§\ref{sec:compatibility-lemmas}) that
lead up to the Fundamental Lemma (Â§\ref{sec:fundamental}).
We conclude with the proof of semantic type safety
in Section~\ref{sec:proof-sem-safety}.

\subsection{Syntax of STLC with Recursive Functions}
\label{sec:STLC-syntax}

This variant of the STLC includes the type of natural numbers and
function types.

\begin{code}
data Type : Set where
  `â„•  : Type
  _â‡’_ : Type â†’ Type â†’ Type
\end{code}

The proof of semantic type safety relies on a lemma regarding
substitution whose proof is quite involved but quite standard.  We can
obtain the substitution lemma for free if we use the Abstract Binding
Tree (ABT) library~\citep{Siek:2021to} to define the syntax of terms.  The
library is parameterized by a type \textsf{Op} that specifies the
constructors and a function \textsf{sig} that describes the arity and
binding structure of each term constructor. For this variant of the
STLC, the terms include lambda abstraction, application, the zero
numeral, the successor operation, case analysis on natural numbers,
and a recursive fixpoint operator.

\begin{code}
data Op : Set where
  op-lam : Op
  op-app : Op
  op-zero : Op
  op-suc : Op
  op-case : Op
  op-rec : Op
\end{code}

Next we define the \textsf{sig} function for this variant of the STLC.
For each \textsf{Op}, it returns a list of \textsf{Sig}, which
specifies the number of variable bindings that are introduced for each
subterm. The â–  means zero bindings and Î½ means add one binding.  So we
see below that lambda abstraction has one subterm with one variable
binding. The \textsf{case} operator has three subterms with one
variable binding for the third subterm.

\begin{code}
sig : Op â†’ List Sig
sig op-lam = (Î½ â– ) âˆ· []
sig op-app = â–  âˆ· â–  âˆ· []
sig op-zero = []
sig op-suc = â–  âˆ· []
sig op-case = â–  âˆ· â–  âˆ· (Î½ â– ) âˆ· []
sig op-rec = (Î½ â– ) âˆ· []
\end{code}

\noindent We import the ABT library to obtain the definition of terms,
whose type we name \textsf{Term}, and we obtain all of the
substitution lemmas provided by the library.

\begin{code}
open import rewriting.AbstractBindingTree Op sig renaming (ABT to Term) public
\end{code}

\noindent The following metavariables range over \textsf{Term}.

\begin{code}
variable L Lâ€² M Mâ€² N Nâ€² V Vâ€² W Wâ€² : Term
\end{code}

The notation for constructing terms from the ABT library is rather
verbose, so we define the following shorthand notations use Agda's
\textsf{pattern} facility.

\begin{code}
pattern Æ› N  = op-lam â¦… cons (bind (ast N)) nil â¦†

infixl 7  _Â·_
pattern _Â·_ L M = op-app â¦… cons (ast L) (cons (ast M) nil) â¦†

pattern `zero = op-zero â¦… nil â¦†
pattern `suc M = op-suc â¦… cons (ast M) nil â¦†

pattern case L M N = op-case â¦… cons (ast L) (cons (ast M) (cons (bind (ast N)) nil)) â¦†

pattern Î¼ N = op-rec â¦… cons (bind (ast N)) nil â¦†
\end{code}


\subsection{Dynamic Semantics of STLC}
\label{sec:STLC-reduction}

The standard reduction semantics for the STLC with recursive functions~\citep{Pierce:2002hj}
includes the following reduction rule for the fixpoint operator.
\[
  Î¼x.M \longrightarrow M[x â†¦ Î¼x.M]
\]
This rule involves the substitution of an arbitrary term (not a
value). Unfortunately, the usual formulation of logical relations for
call-by-value languages requires that substitutions map variables to
values. We therefore use an alternative reduction semantics in which
$Î¼x.V$ is categorized as a value and replace the above reduction
rule with the following one.
\[
(Î¼x.V) \app W \longrightarrow V[x â†¦ Î¼x.V] \app W
\]
To that end, we begin with the following definition of the
\textsf{Value} predicate.

\begin{code}
data Value : Term â†’ Set where
  V-Æ› : Value (Æ› N)
  V-zero : Value `zero
  V-suc : Value V â†’ Value (`suc V)
  V-Î¼ : Value V â†’ Value (Î¼ V)
\end{code}

\noindent The \textsf{value} function extracts the term from
a proof that the term is a value.

\begin{code}
value : Value V â†’ Term
value {V} v = V
\end{code}

\noindent The following lemma is the inversion principle for a
fixpoint value.

\begin{code}
Value-Î¼-inv : Value (Î¼ V) â†’ Value V
Value-Î¼-inv (V-Î¼ v) = v
\end{code}

Our reduction semantics will employ frames, a kind of shallow evaluation context,
which we define as follows.

\begin{code}
infix  6 â–¡Â·_
infix  6 _Â·â–¡

data Frame : Set where
  â–¡Â·_ : Term â†’ Frame
  _Â·â–¡ : Value V â†’ Frame
  sucâ–¡ : Frame
  caseâ–¡ : Term â†’ Term â†’ Frame
\end{code}

\noindent The notation $F âŸ¦ N âŸ§$ is for plugging the term $N$ into
the frame $F$.

\begin{code}
_âŸ¦_âŸ§ : Frame â†’ Term â†’ Term
(â–¡Â· M) âŸ¦ L âŸ§        =  L Â· M
(v Â·â–¡) âŸ¦ M âŸ§        =  value v Â· M
sucâ–¡ âŸ¦ M âŸ§          = `suc M
(caseâ–¡ M N) âŸ¦ L âŸ§   = case L M N
\end{code}

The reduction relation for this STLC are defined as follows.  

\begin{code}
infix 2 _â€”â†’_
data _â€”â†’_ : Term â†’ Term â†’ Set where
  Î²-Æ› : Value W â†’ (Æ› N) Â· W â€”â†’ N [ W ]
  Î²-zero : case `zero M N â€”â†’ M
  Î²-suc : Value V â†’ case (`suc V) M N â€”â†’ N [ V ]
  Î²-Î¼ : Value V â†’ Value W â†’ (Î¼ V) Â· W â€”â†’ V [ Î¼ V ] Â· W
  Î¾Î¾ : âˆ€ {M N : Term} {Mâ€² Nâ€² : Term}
    â†’ (F : Frame) â†’  Mâ€² â‰¡ F âŸ¦ M âŸ§  â†’  Nâ€² â‰¡ F âŸ¦ N âŸ§  â†’  M â€”â†’ N  â†’  Mâ€² â€”â†’ Nâ€²
\end{code}

\noindent The Î¾Î¾ rule will most often be used with \textsf{refl} as
arguments for the second and third premise, so we define the following
shorthand.

\begin{code}
pattern Î¾ F Mâ€”â†’N = Î¾Î¾ F refl refl Mâ€”â†’N
\end{code}

We define \textsf{reducible} in the standard way.

\begin{code}
reducible : (M : Term) â†’ Set
reducible M = âˆƒ[ N ] (M â€”â†’ N)
\end{code}

\noindent Values are not reducible.

\begin{code}
value-irreducible : âˆ€ {V M : Term} â†’ Value V â†’ Â¬ (V â€”â†’ M)
value-irreducible V-Æ› (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible V-Æ› (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible V-Æ› (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
value-irreducible (V-suc v) (Î¾ sucâ–¡ Vâ€”â†’M) = value-irreducible v Vâ€”â†’M
value-irreducible (V-Î¼ v) (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible (V-Î¼ v) (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible (V-Î¼ v) (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
\end{code}

We need the following inversion principle for the reduction of a
fixpoint applied to an argument.

\begin{code}
Î²-Î¼-inv : âˆ€{V W N} â†’ Value V â†’ Value W â†’ Î¼ V Â· W â€”â†’ N  â†’  N â‰¡ V [ Î¼ V ] Â· W
Î²-Î¼-inv v w (Î¾ (â–¡Â· xâ‚‚) r) = âŠ¥-elim (value-irreducible (V-Î¼ v) r)
Î²-Î¼-inv v w (Î¾Î¾ (xâ‚‚ Â·â–¡) refl xâ‚ r) = âŠ¥-elim (value-irreducible w r)
Î²-Î¼-inv v w (Î²-Î¼ x xâ‚) = refl
\end{code}

We define the reflexive and transitive closure of reduction as follows.

\begin{code}
infix  2 _â€”â† _
infixr 2 _â€”â†’âŸ¨_âŸ©_
infix  3 _END

data _â€”â† _ : Term â†’ Term â†’ Set where
  _END : (M : Term) â†’ M â€”â†  M
  _â€”â†’âŸ¨_âŸ©_ : (L : Term) {M N : Term} â†’ L â€”â†’ M  â†’  M â€”â†  N  â†’  L â€”â†  N
\end{code}

\noindent The length of a reduction sequence is computed by the \textsf{len} function.

\begin{code}
len : âˆ€{M N : Term} â†’ (Mâ†’N : M â€”â†  N) â†’ â„•
len (_ END) = 0
len (_ â€”â†’âŸ¨ _ âŸ© red) = suc (len red)
\end{code}


\subsection{Type System of STLC}
\label{sec:STLC-type-system}

\begin{code}
infix 3 _âŠ¢_â¦‚_
data _âŠ¢_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set

infix 3 _âŠ¢â±½_â¦‚_
data _âŠ¢â±½_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
\end{code}


\begin{code}
data _âŠ¢â±½_â¦‚_ where
  âŠ¢â±½zero : âˆ€ {Î“} â†’ Î“ âŠ¢â±½ `zero â¦‚ `â„•
  âŠ¢â±½suc : âˆ€ {Î“ V} â†’ Î“ âŠ¢â±½ V â¦‚ `â„•  â†’  Î“ âŠ¢â±½ `suc V â¦‚ `â„•
  âŠ¢â±½Æ› : âˆ€ {Î“ N A B} â†’ (A âˆ· Î“) âŠ¢ N â¦‚ B  â†’  Î“ âŠ¢â±½ Æ› N â¦‚ (A â‡’ B)
  âŠ¢â±½Î¼ : âˆ€ {Î“ V A B} â†’ (A â‡’ B âˆ· Î“) âŠ¢â±½ V â¦‚ A â‡’ B  â†’  Î“ âŠ¢â±½ Î¼ V â¦‚ A â‡’ B
\end{code}

\begin{code}
data _âŠ¢_â¦‚_ where

  âŠ¢` : âˆ€ {Î“ x A}
    â†’ Î“ âˆ‹ x â¦‚ A
      -----------
    â†’ Î“ âŠ¢ ` x â¦‚ A

  âŠ¢suc : âˆ€ {Î“ M}
    â†’ Î“ âŠ¢ M â¦‚ `â„•
      ---------------
    â†’ Î“ âŠ¢ `suc M â¦‚ `â„•
    
  âŠ¢case : âˆ€{Î“ L M N A}
    â†’ Î“ âŠ¢ L â¦‚ `â„•
    â†’ Î“ âŠ¢ M â¦‚ A
    â†’ `â„• âˆ· Î“ âŠ¢ N â¦‚ A
      ------------------
    â†’ Î“ âŠ¢ case L M N â¦‚ A

  âŠ¢Â· : âˆ€ {Î“ L M A B}
    â†’ Î“ âŠ¢ L â¦‚ (A â‡’ B)
    â†’ Î“ âŠ¢ M â¦‚ A
      -------------
    â†’ Î“ âŠ¢ L Â· M â¦‚ B

  âŠ¢val : âˆ€ {Î“ V A}
    â†’ Î“ âŠ¢â±½ V â¦‚ A
      ----------
    â†’ Î“ âŠ¢ V â¦‚ A
\end{code}

\begin{code}
âŠ¢â±½â‡’Value : âˆ€{Î“ V A} â†’ Î“ âŠ¢â±½ V â¦‚ A â†’ Value V
âŠ¢â±½â‡’Value âŠ¢â±½zero = V-zero
âŠ¢â±½â‡’Value (âŠ¢â±½suc âŠ¢V) = V-suc (âŠ¢â±½â‡’Value âŠ¢V)
âŠ¢â±½â‡’Value (âŠ¢â±½Æ› âŠ¢N) = V-Æ›
âŠ¢â±½â‡’Value (âŠ¢â±½Î¼ âŠ¢V) = V-Î¼ (âŠ¢â±½â‡’Value âŠ¢V)
\end{code}

\subsection{Definition of the Logical Relation}
\label{sec:log-rel}

\begin{code}
â„°âŠğ’±-type : Set
â„°âŠğ’±-type = (Type Ã— Term) âŠ (Type Ã— Term)

â„°âŠğ’±-ctx : Context
â„°âŠğ’±-ctx = â„°âŠğ’±-type âˆ· []
\end{code}



\begin{code}
â„°Ë¢âŸ¦_âŸ§ : Type â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Now âˆ…)
â„°Ë¢âŸ¦ A âŸ§ M = (injâ‚‚ (A , M)) âˆˆ zeroË¢

ğ’±Ë¢âŸ¦_âŸ§ : Type â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Now âˆ…)
ğ’±Ë¢âŸ¦ A âŸ§ V = (injâ‚ (A , V)) âˆˆ zeroË¢
\end{code}

\begin{code}
pre-â„° : Type â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)
pre-ğ’± : Type â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)

pre-â„° A M = (pre-ğ’± A M âŠË¢ (reducible M)Ë¢) Ã—Ë¢ (âˆ€Ë¢[ N ] (M â€”â†’ N)Ë¢ â†’Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ A âŸ§ N))

pre-ğ’± `â„• `zero       = topáµ– Ë¢
pre-ğ’± `â„• (`suc V)    = pre-ğ’± `â„• V
pre-ğ’± (A â‡’ B) (Æ› N)  = âˆ€Ë¢[ W ] â–·Ë¢ (ğ’±Ë¢âŸ¦ A âŸ§ W) â†’Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ B âŸ§ (N [ W ]))
pre-ğ’± (A â‡’ B) (Î¼ V)  = (Value V)Ë¢ Ã—Ë¢ (â–·Ë¢ (ğ’±Ë¢âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))
pre-ğ’± A M            = âŠ¥ Ë¢
\end{code}

\begin{code}
pre-â„°âŠğ’± : â„°âŠğ’±-type â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)
pre-â„°âŠğ’± (injâ‚ (A , V)) = pre-ğ’± A V
pre-â„°âŠğ’± (injâ‚‚ (A , M)) = pre-â„° A M

â„°âŠğ’± : â„°âŠğ’±-type â†’ Setáµ’
â„°âŠğ’± X = Î¼áµ’ pre-â„°âŠğ’± X
\end{code}

\begin{code}
â„°âŸ¦_âŸ§ : Type â†’ Term â†’ Setáµ’
â„°âŸ¦ A âŸ§ M = â„°âŠğ’± (injâ‚‚ (A , M))

ğ’±âŸ¦_âŸ§ : Type â†’ Term â†’ Setáµ’
ğ’±âŸ¦ A âŸ§ V = â„°âŠğ’± (injâ‚ (A , V))
\end{code}


\begin{code}
progress : Type â†’ Term â†’ Setáµ’
progress A M = ğ’±âŸ¦ A âŸ§ M âŠáµ’ (reducible M)áµ’

preservation : Type â†’ Term â†’ Setáµ’
preservation A M = âˆ€áµ’[ N ] ((M â€”â†’ N)áµ’ â†’áµ’ â–·áµ’ (â„°âŸ¦ A âŸ§ N))
\end{code}

\begin{code}
open import EquivalenceRelation public
\end{code}

\begin{code}
â„°-stmt : âˆ€{A}{M} â†’ â„°âŸ¦ A âŸ§ M â‰¡áµ’ progress A M Ã—áµ’ preservation A M
â„°-stmt {A}{M} =
  â„°âŸ¦ A âŸ§ M                                 â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  Î¼áµ’ pre-â„°âŠğ’± (injâ‚‚ (A , M))               â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± (injâ‚‚ (A , M)) âŸ©
  â™¯ (pre-â„°âŠğ’± (injâ‚‚ (A , M))) (â„°âŠğ’± , ttáµ–) â©¦âŸ¨ cong-Ã—áµ’ (cong-âŠáµ’ (â‰¡áµ’-sym (fixpointáµ’ pre-â„°âŠğ’± (injâ‚ (A , M))))
                                                  (â‰¡áµ’-refl refl)) (â‰¡áµ’-refl refl) âŸ©
  progress A M Ã—áµ’ preservation A M         âˆ
\end{code}

\begin{code}
â„°-intro : âˆ€ {ğ’«}{A}{M} â†’ ğ’« âŠ¢áµ’ progress A M â†’ ğ’« âŠ¢áµ’ preservation A M â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ M
â„°-intro ğ’«âŠ¢prog ğ’«âŠ¢pres = substáµ’ (â‰¡áµ’-sym â„°-stmt) (ğ’«âŠ¢prog ,áµ’ ğ’«âŠ¢pres)

â„°-progress : âˆ€ {ğ’«}{A}{M} â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ M â†’ ğ’« âŠ¢áµ’ progress A M
â„°-progress ğ’«âŠ¢â„°M = projâ‚áµ’ (substáµ’ â„°-stmt ğ’«âŠ¢â„°M )

â„°-preservation : âˆ€ {ğ’«}{A}{M} â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ M â†’ ğ’« âŠ¢áµ’ preservation A M
â„°-preservation ğ’«âŠ¢â„°M = projâ‚‚áµ’ (substáµ’ â„°-stmt ğ’«âŠ¢â„°M )
\end{code}

\begin{code}
ğ’±-suc : âˆ€{M} â†’ ğ’±âŸ¦ `â„• âŸ§ (`suc M) â‰¡áµ’ ğ’±âŸ¦ `â„• âŸ§ M
ğ’±-suc {M} = let X = injâ‚ (`â„• , `suc M) in
  ğ’±âŸ¦ `â„• âŸ§ (`suc M)              â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  â„°âŠğ’± X                         â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X âŸ©
  â™¯ (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)     â©¦âŸ¨ â‰¡áµ’-sym (fixpointáµ’ pre-â„°âŠğ’± (injâ‚ (`â„• , M))) âŸ© 
  ğ’±âŸ¦ `â„• âŸ§ M                     âˆ
\end{code}

\begin{code}
ğ’±-fun : âˆ€{A B}{N} â†’ ğ’±âŸ¦ A â‡’ B âŸ§ (Æ› N) â‰¡áµ’ (âˆ€áµ’[ W ] ((â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))))
ğ’±-fun {A}{B}{N} =
   let X = (injâ‚ (A â‡’ B , Æ› N)) in
   ğ’±âŸ¦ A â‡’ B âŸ§ (Æ› N)                                         â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   â„°âŠğ’± X                                                    â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X âŸ©
   â™¯ (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)                               â©¦âŸ¨ â‰¡áµ’-refl refl âŸ© 
   (âˆ€áµ’[ W ] ((â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))))   âˆ
\end{code}

\begin{code}
ğ’±-Î¼ : âˆ€{A B}{V} â†’ ğ’±âŸ¦ A â‡’ B âŸ§ (Î¼ V) â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))
ğ’±-Î¼ {A}{B}{V} =
   let X = (injâ‚ (A â‡’ B , Î¼ V)) in
   ğ’±âŸ¦ A â‡’ B âŸ§ (Î¼ V)                                         â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   â„°âŠğ’± X                                                    â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X âŸ©
   â™¯ (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)                               â©¦âŸ¨ â‰¡áµ’-refl refl âŸ© 
   (Value V)áµ’ Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))              âˆ
\end{code}

\begin{code}
safe-body : List Setáµ’ â†’ Term â†’ Type â†’ Type â†’ Set
safe-body ğ’« N A B = âˆ€{W} â†’ ğ’« âŠ¢áµ’ (â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))

ğ’±-fun-elim : âˆ€{ğ’«}{A}{B}{V}{R}
   â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A â‡’ B âŸ§ V
   â†’ (âˆ€ N â†’ V â‰¡ Æ› N â†’ safe-body ğ’« N A B â†’ ğ’« âŠ¢áµ’ R)
   â†’ (âˆ€ Vâ€² â†’ V â‰¡ Î¼ Vâ€² â†’ ğ’« âŠ¢áµ’ â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (Vâ€² [ V ])) â†’ ğ’« âŠ¢áµ’ R)
    ---------------------------------------------------------------
   â†’ ğ’« âŠ¢áµ’ R
ğ’±-fun-elim {ğ’«}{A}{B}{V}{R} âŠ¢ğ’±V contÎ» contÎ¼ =
  âŠ¢áµ’-sucP âŠ¢ğ’±V Î» { ğ’±Vsn â†’ aux {V} ğ’±Vsn âŠ¢ğ’±V contÎ» contÎ¼}
  where
  aux : âˆ€{V}{n}
     â†’ # (ğ’±âŸ¦ A â‡’ B âŸ§ V) (suc n)
     â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A â‡’ B âŸ§ V
     â†’ (âˆ€ N â†’ V â‰¡ Æ› N â†’ safe-body ğ’« N A B â†’ ğ’« âŠ¢áµ’ R)
     â†’ (âˆ€ Vâ€² â†’ V â‰¡ Î¼ Vâ€² â†’ ğ’« âŠ¢áµ’ â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (Vâ€² [ V ])) â†’ ğ’« âŠ¢áµ’ R)
     â†’ ğ’« âŠ¢áµ’ R
  aux{Æ› N}{n} ğ’±V âŠ¢ğ’±V contÎ» contÎ¼ = contÎ» N refl Î» {W} â†’
      instáµ’{Ï•áµƒ = Î» W â†’ (â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))}
                 (substáµ’ ğ’±-fun âŠ¢ğ’±V) W
  aux{Î¼ V}{n} ğ’±V âŠ¢ğ’±V contÎ» contÎ¼ = contÎ¼ V refl (âŠ¢áµ’-intro
     Î» { zero ğ’«k â†’ tt
       ; (suc k) â†’ Î» ğ’«sk â†’ let (v , ğ’±V[Î¼V]) = âŠ¢áµ’-elim âŠ¢ğ’±V (suc k) ğ’«sk in  ğ’±V[Î¼V]})
\end{code}

\begin{code}
ğ’±â‡’Value : âˆ€ {k} A M â†’ # (ğ’±âŸ¦ A âŸ§ M) (suc k) â†’ Value M
ğ’±â‡’Value `â„• `zero ğ’±M = V-zero
ğ’±â‡’Value `â„• (`suc M) ğ’±M = V-suc (ğ’±â‡’Value `â„• M ğ’±M)
ğ’±â‡’Value (A â‡’ B) (Æ› N) ğ’±M = V-Æ›
ğ’±â‡’Value (A â‡’ B) (Î¼ V) (v , â–·ğ’±V[Î¼V]) = V-Î¼ v
\end{code}

\begin{code}
ğ’±â‡’â„° : âˆ€{A}{ğ’«}{V}
   â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A âŸ§ V
     ---------------
   â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ V
ğ’±â‡’â„° {A}{ğ’«}{V} ğ’«âŠ¢ğ’±V = â„°-intro prog pres
    where
    prog = injâ‚áµ’ ğ’«âŠ¢ğ’±V
    pres = Î›áµ’[ N ] â†’áµ’I (constáµ’E Záµ’ Î» Vâ€”â†’N â†’
             âŠ¢áµ’-sucP (Sáµ’ ğ’«âŠ¢ğ’±V) Î» ğ’±V â†’
                âŠ¥-elim (value-irreducible (ğ’±â‡’Value A V ğ’±V ) Vâ€”â†’N))
\end{code}

\subsection{Definition of Semantic Type Safety for Open Terms}
\label{sec:sem-type-safety}


\begin{code}
ğ“–âŸ¦_âŸ§ : (Î“ : List Type) â†’ Subst â†’ List Setáµ’
ğ“–âŸ¦ [] âŸ§ Ïƒ = []
ğ“–âŸ¦ A âˆ· Î“ âŸ§ Ïƒ = (ğ’±âŸ¦ A âŸ§ (Ïƒ 0)) âˆ· ğ“–âŸ¦ Î“ âŸ§ (Î» x â†’ Ïƒ (suc x))
\end{code}

\begin{code}
infix 3 _âŠ¨_â¦‚_
_âŠ¨_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
Î“ âŠ¨ M â¦‚ A = âˆ€ (Î³ : Subst) â†’ ğ“–âŸ¦ Î“ âŸ§ Î³ âŠ¢áµ’ â„°âŸ¦ A âŸ§ (âŸª Î³ âŸ« M)
\end{code}

\begin{code}
infix 3 _âŠ¨â±½_â¦‚_
_âŠ¨â±½_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
Î“ âŠ¨â±½ V â¦‚ A = âˆ€ (Î³ : Subst) â†’ ğ“–âŸ¦ Î“ âŸ§ Î³ âŠ¢áµ’ ğ’±âŸ¦ A âŸ§ (âŸª Î³ âŸ« V)
\end{code}


