\begin{comment}
\begin{code}
{-# OPTIONS --rewriting #-}

module July2023.STLC where

open import Data.List using (List; []; _âˆ·_; length)
open import Data.Nat
open import Data.Nat.Induction
open import Data.Bool using (true; false) renaming (Bool to ğ”¹)
open import Data.List using (map)
open import Data.Nat.Properties
open import Data.Product using (_,_;_Ã—_; projâ‚; projâ‚‚; Î£-syntax; âˆƒ-syntax)
--open import Data.Unit.Polymorphic using (âŠ¤; tt)
open import Data.Unit using (âŠ¤; tt)
open import Data.Unit.Polymorphic renaming (âŠ¤ to topáµ–; tt to ttáµ–)
open import Data.Vec using (Vec) renaming ([] to []ÌŒ; _âˆ·_ to _âˆ·ÌŒ_)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Induction using (RecStruct)
open import Induction.WellFounded as WF
open import Data.Product.Relation.Binary.Lex.Strict
  using (Ã—-Lex; Ã—-wellFounded; Ã—-preorder)
open import Relation.Binary using (Rel)
open import Relation.Binary.PropositionalEquality as Eq
  using (_â‰¡_; _â‰¢_; refl; sym; cong; congâ‚‚; subst; trans)
--open Eq.â‰¡-Reasoning
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Sig
open import Var
open import July2023.StepIndexedLogic
open import EquivalenceRelation public

\end{code}
\end{comment}

\section{Case Study: Type Safety of the STLC with Recursive Functions}
\label{sec:STLC}

We provide an example application of our Step-Indexed Logic with a
case study in proving semantic type safety for the STLC with recursive
functions. We choose to extend STLC with recursive functions because
otherwise, one does not need step-indexed logical relations; logical
relations that are only indexed by types are sufficient.  The next few
subsections give the definition of this variant of the STLC (syntax in
Â§\ref{sec:STLC-syntax}, reduction semantics in
Â§\ref{sec:STLC-reduction}, type system in
Â§\ref{sec:STLC-type-system}). We then define the step-indexed logical
relation in Section~\ref{sec:log-rel} and use it to define semantic
type safety in Section~\ref{sec:sem-type-safety}. The rest of the
subsections give the proof of semantic type safety, starting
with the Bind Lemma (Â§\ref{sec:bind-lemma}), then the many
Compatibility Lemmas (Â§\ref{sec:compatibility-lemmas}) that
lead up to the Fundamental Lemma (Â§\ref{sec:fundamental}).
We conclude with the proof of semantic type safety
in Section~\ref{sec:proof-sem-safety}.

\subsection{Syntax of STLC with Recursive Functions}
\label{sec:STLC-syntax}

This variant of the STLC includes the type of natural numbers and
function types.

\begin{code}
data Type : Set where
  `â„•  : Type
  _â‡’_ : Type â†’ Type â†’ Type
\end{code}

Our definition of STLC terms is isomorphic to the following data type.

\begin{code}
module AsIf where
  data Term : Set where
    `_ : Var â†’ Term
    Æ› : Term â†’ Term
    _Â·_ : Term â†’ Term â†’ Term
    `zero : Term
    `suc : Term â†’ Term
    case : Term â†’ Term â†’ Term â†’ Term
    Î¼ : Term â†’ Term
\end{code}

\noindent Instead of using the above data type, we instead use
Abstract Binding Tree (ABT) library~\citep{Siek:2021to} to define the
syntax of terms. The reason is that the proof of semantic type safety
relies on a lemma regarding substitution whose proof is quite involved
but standard.  We can obtain this substitution lemma for free if we
use the ABT library.

The ABT library is parameterized by a type \textsf{Op} that specifies
the constructors and a function \textsf{sig} that describes the arity
and binding structure of each term constructor. For this variant of
the STLC, the terms include lambda abstraction, application, the zero
numeral, the successor operation, case analysis on natural numbers,
and a recursive fixpoint operator. The ABT library automatically
includes a constructor for variables (de Bruijn indices), so we do not
need to include them in \textsf{Op}.

\begin{code}
data Op : Set where
  op-lam : Op
  op-app : Op
  op-zero : Op
  op-suc : Op
  op-case : Op
  op-rec : Op
\end{code}

Next we define the \textsf{sig} function for this variant of the STLC.
For each \textsf{Op}, it returns a list of \textsf{Sig}, which
specifies the number of variable bindings that are introduced for each
subterm. The â–  means zero bindings and Î½ means add one binding.  So we
see below that lambda abstraction has one subterm with one variable
binding. The \textsf{case} operator has three subterms with one
variable binding for the third subterm.

\begin{code}
sig : Op â†’ List Sig
sig op-lam = (Î½ â– ) âˆ· []
sig op-app = â–  âˆ· â–  âˆ· []
sig op-zero = []
sig op-suc = â–  âˆ· []
sig op-case = â–  âˆ· â–  âˆ· (Î½ â– ) âˆ· []
sig op-rec = (Î½ â– ) âˆ· []
\end{code}

\noindent We import the ABT library to obtain the definition of terms,
whose type we name \textsf{Term}, the definition of substitution,
and lemmas about substitution.

\begin{code}
open import rewriting.AbstractBindingTree Op sig renaming (ABT to Term) public
\end{code}

\noindent The following metavariables range over \textsf{Term}.

\begin{code}
variable L Lâ€² M Mâ€² N Nâ€² V Vâ€² W Wâ€² : Term
\end{code}

The notation for constructing terms from the ABT library is rather
verbose, so we define the following shorthand notations use Agda's
\textsf{pattern} facility.

\begin{code}
pattern Æ› N  = op-lam â¦… cons (bind (ast N)) nil â¦†

infixl 7  _Â·_
pattern _Â·_ L M = op-app â¦… cons (ast L) (cons (ast M) nil) â¦†

pattern `zero = op-zero â¦… nil â¦†
pattern `suc M = op-suc â¦… cons (ast M) nil â¦†

pattern case L M N = op-case â¦… cons (ast L) (cons (ast M) (cons (bind (ast N)) nil)) â¦†

pattern Î¼ N = op-rec â¦… cons (bind (ast N)) nil â¦†
\end{code}

The ABT library defines a bracket notation for substitution. For example, the
follow substitution replaces the de Bruijn index zero with the term $N$.
\begin{code}
_ : (` 0) [ N ] â‰¡ N
_ = refl
\end{code}

\noindent When substitution is applied to a non-zero variable, its de
Bruijn index is decreased by one. The reason for this is that one
typically performs substitution when removing a variable binding, as
we shall see in the Î²-Æ› reduction rule in the next section.

\begin{code}
_ = example where
  example : âˆ€ N â†’ (` 1) [ N ] â‰¡ ` 0
  example N rewrite sub-var (N â€¢ id) 1 = refl
\end{code}

The ABT library also defines parallel substitution, which takes a function Ïƒ from natural
numbers to terms, and applies it to a term $M$ by replacing all the free variables in $M$
according to Ïƒ, written $âŸª Ïƒ âŸ« M$. We refer to a function from natural numbers to terms
as a \emph{substitution}. The identity substitution is named \textsf{id}.

\begin{code}
_ : âŸª id âŸ« (` 0) â‰¡ ` 0
_ = refl
\end{code}

\noindent The ABT library defines a cons-like operator on substitutions, written $M â€¢ Ïƒ$,
that maps $0$ to $M$ and an other $x$ to $Ïƒ\, (x \minus 1)$. Here are a few example
that demonstrate the application of the substitution $M â€¢ N â€¢ \mathsf{id}$ to several different
variables.

\begin{code}
_ : âˆ€{M N} â†’ âŸª M â€¢ N â€¢ id âŸ« (` 0) â‰¡ M
_ = refl

_ = example where
  example : âˆ€ M N â†’ âŸª M â€¢ N â€¢ id âŸ« (` 1) â‰¡ N
  example M N rewrite sub-var (M â€¢ N â€¢ id) 1 = refl

_ = example where
  example : âˆ€ M N â†’ âŸª M â€¢ N â€¢ id âŸ« (` 2) â‰¡ ` 0
  example M N rewrite sub-var (M â€¢ N â€¢ id) 2 = refl
\end{code}

\noindent The â†‘ operator increments the de Bruijn indices.

\begin{code}
_ = example where
  example : âŸª â†‘ âŸ« (` 0) â‰¡ ` 1
  example rewrite sub-var â†‘ 0 | ren-def suc 0 = refl
\end{code}

\noindent The sequencing operator (Ïƒ â¨Ÿ Ï„) creates a substitution that is equivalent
to applying Ïƒ and then Ï„.

\begin{code}
_ : âˆ€ Ïƒ Ï„ M â†’ âŸª Ïƒ â¨Ÿ Ï„ âŸ« M â‰¡ âŸª Ï„ âŸ« (âŸª Ïƒ âŸ« M)
_ = Î» Ïƒ Ï„ M â†’ refl
\end{code}

\noindent The \textsf{ext} operator transports a substitution
underneath one variable binder. For example, applying the substitution
Ïƒ to a lambda abstraction pushes through to apply $\mathsf{ext}\,Ïƒ$ to
its body.

\begin{code}
_ : âˆ€ Ïƒ N â†’ âŸª Ïƒ âŸ« (Æ› N) â‰¡ Æ› (âŸª ext Ïƒ âŸ« N)
_ = Î» Ïƒ N â†’ refl
\end{code}

\noindent The \textsf{ext} operator is equivalent to 

\begin{code}
_ : âˆ€ Ïƒ â†’ ext Ïƒ â‰¡ ` 0 â€¢ (Ïƒ â¨Ÿ â†‘)
_ = Î» Ïƒ â†’ refl
\end{code}

\noindent The following is the substitution lemma that we shall need
from the ABT library.

\begin{code}
_ : âˆ€ Ïƒ N V â†’ (âŸª ext Ïƒ âŸ« N) [ V ] â‰¡ âŸª V â€¢ Ïƒ âŸ« N
_ = Î» Ïƒ N V â†’ exts-sub-cons {Ïƒ}{N}{V}
\end{code}


\subsection{Dynamic Semantics of STLC}
\label{sec:STLC-reduction}

The standard reduction semantics for the STLC with recursive functions~\citep{Pierce:2002hj}
includes the following reduction rule for the fixpoint operator.
\[
  Î¼x.M \longrightarrow M[x â†¦ Î¼x.M]
\]
This rule involves the substitution of an arbitrary term (not a
value). Unfortunately, the usual formulation of logical relations for
call-by-value languages requires that substitutions map variables to
values. We therefore use an alternative reduction semantics in which
$Î¼x.V$ is categorized as a value and replace the above reduction
rule with the following one.
\[
(Î¼x.V) \app W \longrightarrow V[x â†¦ Î¼x.V] \app W
\]
To that end, we begin with the following definition of the
\textsf{Value} predicate.

\begin{code}
data Value : Term â†’ Set where
  V-Æ› : Value (Æ› N)
  V-zero : Value `zero
  V-suc : Value V â†’ Value (`suc V)
  V-Î¼ : Value V â†’ Value (Î¼ V)
\end{code}

\noindent The \textsf{value} function extracts the term from
a proof that the term is a value.

\begin{code}
value : Value V â†’ Term
value {V} v = V
\end{code}

\noindent The following lemma is the inversion principle for a
fixpoint value.

\begin{code}
Value-Î¼-inv : Value (Î¼ V) â†’ Value V
Value-Î¼-inv (V-Î¼ v) = v
\end{code}

The result of applying a sustitution to a value is also a value.

\begin{code}
subst-preserves-value : âˆ€ Ïƒ V â†’ Value V â†’ Value (âŸª Ïƒ âŸ« V)
subst-preserves-value Ïƒ .(Æ› _) V-Æ› = V-Æ›
subst-preserves-value Ïƒ .`zero V-zero = V-zero
subst-preserves-value Ïƒ (`suc V) (V-suc v) = V-suc (subst-preserves-value Ïƒ V v)
subst-preserves-value Ïƒ (Î¼ V) (V-Î¼ v) = V-Î¼ (subst-preserves-value (ext Ïƒ) V v)
\end{code}

Our reduction semantics will employ frames, a kind of shallow evaluation context,
which we define as follows.

\begin{code}
infix  6 â–¡Â·_
infix  6 _Â·â–¡

data Frame : Set where
  â–¡Â·_ : Term â†’ Frame
  _Â·â–¡ : Value V â†’ Frame
  sucâ–¡ : Frame
  caseâ–¡ : Term â†’ Term â†’ Frame
\end{code}

\noindent The notation $F âŸ¦ N âŸ§$ is for plugging the term $N$ into
the frame $F$.

\begin{code}
_âŸ¦_âŸ§ : Frame â†’ Term â†’ Term
(â–¡Â· M) âŸ¦ L âŸ§        =  L Â· M
(v Â·â–¡) âŸ¦ M âŸ§        =  value v Â· M
sucâ–¡ âŸ¦ M âŸ§          = `suc M
(caseâ–¡ M N) âŸ¦ L âŸ§   = case L M N
\end{code}

The reduction relation for this STLC are defined as follows.  

\begin{code}
infix 2 _â€”â†’_
data _â€”â†’_ : Term â†’ Term â†’ Set where
  Î²-Æ› : Value W â†’ (Æ› N) Â· W â€”â†’ N [ W ]
  Î²-zero : case `zero M N â€”â†’ M
  Î²-suc : Value V â†’ case (`suc V) M N â€”â†’ N [ V ]
  Î²-Î¼ : Value V â†’ Value W â†’ (Î¼ V) Â· W â€”â†’ V [ Î¼ V ] Â· W
  Î¾Î¾ : (F : Frame) â†’  Mâ€² â‰¡ F âŸ¦ M âŸ§  â†’  Nâ€² â‰¡ F âŸ¦ N âŸ§  â†’  M â€”â†’ N  â†’  Mâ€² â€”â†’ Nâ€²
\end{code}

\noindent The Î¾Î¾ rule will most often be used with \textsf{refl} as
arguments for the second and third premise, so we define the following
shorthand.

\begin{code}
pattern Î¾ F Mâ€”â†’N = Î¾Î¾ F refl refl Mâ€”â†’N
\end{code}

We define \textsf{reducible} in the standard way.

\begin{code}
reducible : (M : Term) â†’ Set
reducible M = âˆƒ[ N ] (M â€”â†’ N)
\end{code}

\noindent Values are not reducible.

\begin{code}
value-irreducible : âˆ€ {V M : Term} â†’ Value V â†’ Â¬ (V â€”â†’ M)
value-irreducible V-Æ› (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible V-Æ› (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible V-Æ› (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible V-zero (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
value-irreducible (V-suc v) (Î¾ sucâ–¡ Vâ€”â†’M) = value-irreducible v Vâ€”â†’M
value-irreducible (V-Î¼ v) (Î¾Î¾ (â–¡Â· xâ‚‚) () xâ‚ Vâ€”â†’M)
value-irreducible (V-Î¼ v) (Î¾Î¾ (xâ‚‚ Â·â–¡) () xâ‚ Vâ€”â†’M)
value-irreducible (V-Î¼ v) (Î¾Î¾ sucâ–¡ () xâ‚ Vâ€”â†’M)
\end{code}

We need the following inversion principle for the reduction of a
fixpoint applied to an argument.

\begin{code}
Î²-Î¼-inv : âˆ€{V W N} â†’ Value V â†’ Value W â†’ Î¼ V Â· W â€”â†’ N  â†’  N â‰¡ V [ Î¼ V ] Â· W
Î²-Î¼-inv v w (Î¾ (â–¡Â· xâ‚‚) r) = âŠ¥-elim (value-irreducible (V-Î¼ v) r)
Î²-Î¼-inv v w (Î¾Î¾ (xâ‚‚ Â·â–¡) refl xâ‚ r) = âŠ¥-elim (value-irreducible w r)
Î²-Î¼-inv v w (Î²-Î¼ x xâ‚) = refl
\end{code}

We define the reflexive and transitive closure of reduction as follows.

\begin{code}
infix  2 _â€”â† _
infixr 2 _â€”â†’âŸ¨_âŸ©_
infix  3 _END

data _â€”â† _ : Term â†’ Term â†’ Set where
  _END : (M : Term) â†’ M â€”â†  M
  _â€”â†’âŸ¨_âŸ©_ : (L : Term) {M N : Term} â†’ L â€”â†’ M  â†’  M â€”â†  N  â†’  L â€”â†  N
\end{code}

\noindent The length of a reduction sequence is computed by the \textsf{len} function.

\begin{code}
len : âˆ€{M N : Term} â†’ (Mâ†’N : M â€”â†  N) â†’ â„•
len (_ END) = 0
len (_ â€”â†’âŸ¨ _ âŸ© red) = suc (len red)
\end{code}


\subsection{Type System of STLC}
\label{sec:STLC-type-system}

The type system for this STLC is comprised of two mutually recursive
judgments, one for values and one for terms.

\begin{code}
infix 3 _âŠ¢â±½_â¦‚_
data _âŠ¢â±½_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set

infix 3 _âŠ¢_â¦‚_
data _âŠ¢_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
\end{code}

\noindent We define the well-typed values as follows. Note that we
restrict the fixpoint operator to only be applicable to functions.

\begin{code}
data _âŠ¢â±½_â¦‚_ where
  âŠ¢â±½zero : âˆ€ {Î“} â†’ Î“ âŠ¢â±½ `zero â¦‚ `â„•
  âŠ¢â±½suc : âˆ€ {Î“ V} â†’ Î“ âŠ¢â±½ V â¦‚ `â„•  â†’  Î“ âŠ¢â±½ `suc V â¦‚ `â„•
  âŠ¢â±½Æ› : âˆ€ {Î“ N A B} â†’ (A âˆ· Î“) âŠ¢ N â¦‚ B  â†’  Î“ âŠ¢â±½ Æ› N â¦‚ (A â‡’ B)
  âŠ¢â±½Î¼ : âˆ€ {Î“ V A B} â†’ (A â‡’ B âˆ· Î“) âŠ¢â±½ V â¦‚ A â‡’ B  â†’  Î“ âŠ¢â±½ Î¼ V â¦‚ A â‡’ B
\end{code}

\noindent The well-typed values are of course a subset of the values.

\begin{code}
âŠ¢â±½â‡’Value : âˆ€{Î“ V A} â†’ Î“ âŠ¢â±½ V â¦‚ A â†’ Value V
âŠ¢â±½â‡’Value âŠ¢â±½zero = V-zero
âŠ¢â±½â‡’Value (âŠ¢â±½suc âŠ¢V) = V-suc (âŠ¢â±½â‡’Value âŠ¢V)
âŠ¢â±½â‡’Value (âŠ¢â±½Æ› âŠ¢N) = V-Æ›
âŠ¢â±½â‡’Value (âŠ¢â±½Î¼ âŠ¢V) = V-Î¼ (âŠ¢â±½â‡’Value âŠ¢V)
\end{code}

The type system for terms is define below. The notation $Î“ âˆ‹ x â¦‚ A$ is defined
in the ABT library and simply means that $A$ is the type at position $x$ of Î“.
The rule ${âŠ¢}{\mathsf{val}}$ turns a well-typed value into a well-typed term.
The other typing rules are standard.

\begin{code}
data _âŠ¢_â¦‚_ where
  âŠ¢` : âˆ€ {Î“ x A} â†’ Î“ âˆ‹ x â¦‚ A  â†’  Î“ âŠ¢ ` x â¦‚ A
  âŠ¢suc : âˆ€ {Î“ M} â†’ Î“ âŠ¢ M â¦‚ `â„•  â†’  Î“ âŠ¢ `suc M â¦‚ `â„•
  âŠ¢case : âˆ€{Î“ L M N A} â†’ Î“ âŠ¢ L â¦‚ `â„•  â†’  Î“ âŠ¢ M â¦‚ A  â†’  `â„• âˆ· Î“ âŠ¢ N â¦‚ A  â†’  Î“ âŠ¢ case L M N â¦‚ A
  âŠ¢Â· : âˆ€ {Î“ L M A B} â†’ Î“ âŠ¢ L â¦‚ (A â‡’ B)  â†’  Î“ âŠ¢ M â¦‚ A  â†’  Î“ âŠ¢ L Â· M â¦‚ B
  âŠ¢val : âˆ€ {Î“ V A} â†’ Î“ âŠ¢â±½ V â¦‚ A  â†’  Î“ âŠ¢ V â¦‚ A
\end{code}


\subsection{Definition of the Logical Relation}
\label{sec:log-rel}

The logical relation is comprised of two mutually recursive
predicates, ğ’± for values and â„° for terms. The intuitive meaning
of the predicates is that, for a given type $A$, 
$ğ’±âŸ¦ A âŸ§\, V$ means that value $V$ is well behaved according to type $A$
and $â„°âŸ¦ A âŸ§\, M$ means that $M$ is well behaved according to type $A$.

SIL does not directly support mutual recursion, but we can merge the
two predicates into one predicate named ğ’±âŠâ„°, using a sum type.  We
define ğ’±âŠâ„° by an application of Î¼áµ’, so we first need to define the
non-recursive version of ğ’±âŠâ„°, which we call \textsf{pre}-ğ’±âŠâ„°, defined
below. It simply dispatches to the non-recursive \textsf{pre}-â„° and
\textsf{pre}-â„° which we will get to shortly.

\begin{code}
Î“â‚ : Context
Î“â‚ = ((Type Ã— Term) âŠ (Type Ã— Term)) âˆ· []

pre-â„° : Type â†’ Term â†’ SetË¢ Î“â‚ (Later âˆ· [])
pre-ğ’± : Type â†’ Term â†’ SetË¢ Î“â‚ (Later âˆ· [])

pre-ğ’±âŠâ„° : ((Type Ã— Term) âŠ (Type Ã— Term)) â†’ SetË¢ Î“â‚ (Later âˆ· [])
pre-ğ’±âŠâ„° (injâ‚ (A , V)) = pre-ğ’± A V
pre-ğ’±âŠâ„° (injâ‚‚ (A , M)) = pre-â„° A M

ğ’±âŠâ„° : ((Type Ã— Term) âŠ (Type Ã— Term)) â†’ Setáµ’
ğ’±âŠâ„° X = Î¼áµ’ pre-ğ’±âŠâ„° X

ğ’±âŸ¦_âŸ§ : Type â†’ Term â†’ Setáµ’
ğ’±âŸ¦ A âŸ§ V = ğ’±âŠâ„° (injâ‚ (A , V))

â„°âŸ¦_âŸ§ : Type â†’ Term â†’ Setáµ’
â„°âŸ¦ A âŸ§ M = ğ’±âŠâ„° (injâ‚‚ (A , M))
\end{code}

When we use ğ’± and â„° recursively in their definition, we do so by using
the membership operator of SIL with a de Bruijn index to select
which recursive predicate we wish to refer to. In this case, there is
only one recursive predicate in scope and its de Bruijn index is zero.
We define the following shorthand notation for refering to ğ’± and â„°.

\begin{code}
ğ’±Ë¢âŸ¦_âŸ§ : Type â†’ Term â†’ SetË¢ Î“â‚ (Now âˆ· [])
ğ’±Ë¢âŸ¦ A âŸ§ V = injâ‚ (A , V) âˆˆ zeroË¢

â„°Ë¢âŸ¦_âŸ§ : Type â†’ Term â†’ SetË¢ Î“â‚ (Now âˆ· [])
â„°Ë¢âŸ¦ A âŸ§ M = injâ‚‚ (A , M) âˆˆ zeroË¢
\end{code}

The definition of \textsf{pre-â„°}, i.e., what it means for a term to be
well behaved, is essentially a statement of what we'd like to prove:
``progress'' and ``preservation''. In particular, the progress part
says that either $M$ is a well-behaved value or it is reducible. The
preservation part says that if $M$ reduces to $N$, then $N$ is also
well behaved. Note that we insert the â–·Ë¢ operator in front of the
recursive use of â„° because when we use the Î¼áµ’ operator, it will
require the zero de Bruijn index to be at time \textsf{Later}.

\begin{code}
pre-â„° A M = (pre-ğ’± A M âŠË¢ (reducible M)Ë¢) Ã—Ë¢ (âˆ€Ë¢[ N ] (M â€”â†’ N)Ë¢ â†’Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ A âŸ§ N))
\end{code}

The definition of \textsf{pre-ğ’±} defines what it means to be a
well-behaved value according to a given type. For type â„•, the value
must be the literal for zero or be the successor of a well-behaved
value at type â„•. For function types, the value must be either a lambda
abstraction or fixpoint. In the former case, given an arbitrary
well-behaved values $W$, substituting it into the lambda's body $N$
produces a well-behaved term.  In the later case, for the fixpoint
value $Î¼\,V$, the $V$ must be a value (syntactically) and substituting
the whose fixpoint value into $V$ produces a well-behaved value. We
insert the â–·Ë¢ operator around each recursive use of ğ’± and â„°.

\begin{code}
pre-ğ’± `â„• `zero       = topáµ– Ë¢
pre-ğ’± `â„• (`suc V)    = pre-ğ’± `â„• V
pre-ğ’± (A â‡’ B) (Æ› N)  = âˆ€Ë¢[ W ] â–·Ë¢ (ğ’±Ë¢âŸ¦ A âŸ§ W) â†’Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ B âŸ§ (N [ W ]))
pre-ğ’± (A â‡’ B) (Î¼ V)  = (Value V)Ë¢ Ã—Ë¢ (â–·Ë¢ (ğ’±Ë¢âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))
pre-ğ’± A M            = âŠ¥ Ë¢
\end{code}

Next we prove several lemmas that encapsulate uses of the fixpoint
theorem.  We define the following shorthand for referring to the two
parts of the â„° predicate.

\begin{code}
progress : Type â†’ Term â†’ Setáµ’
progress A M = ğ’±âŸ¦ A âŸ§ M âŠáµ’ (reducible M)áµ’

preservation : Type â†’ Term â†’ Setáµ’
preservation A M = âˆ€áµ’[ N ] ((M â€”â†’ N)áµ’ â†’áµ’ â–·áµ’ (â„°âŸ¦ A âŸ§ N))
\end{code}

\noindent The first lemma states that $â„°âŸ¦ A âŸ§ M$ is equivalent to the
conjunction of progress and preservation. The proof uses the fixpoint
theorem twice, once to unfold the definintion of â„° and then again
to fold the definition of ğ’±.

\begin{code}
â„°-stmt : âˆ€{A}{M} â†’ â„°âŸ¦ A âŸ§ M â‰¡áµ’ progress A M Ã—áµ’ preservation A M
â„°-stmt {A}{M} =
  â„°âŸ¦ A âŸ§ M                                 â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  Î¼áµ’ pre-ğ’±âŠâ„° (injâ‚‚ (A , M))                â©¦âŸ¨ fixpointáµ’ pre-ğ’±âŠâ„° (injâ‚‚ (A , M)) âŸ©
  â™¯ (pre-ğ’±âŠâ„° (injâ‚‚ (A , M))) (ğ’±âŠâ„° , ttáµ–)
      â©¦âŸ¨ cong-Ã—áµ’ (cong-âŠáµ’ (â‰¡áµ’-sym (fixpointáµ’ pre-ğ’±âŠâ„° (injâ‚ (A , M)))) (â‰¡áµ’-refl refl)) (â‰¡áµ’-refl refl) âŸ©
  progress A M Ã—áµ’ preservation A M           âˆ
\end{code}

\noindent The following introduction rule for â„° is a directly
corollary of the above.

\begin{code}
â„°-intro : âˆ€ {ğ’«}{A}{M} â†’ ğ’« âŠ¢áµ’ progress A M â†’ ğ’« âŠ¢áµ’ preservation A M â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ M
â„°-intro ğ’«âŠ¢prog ğ’«âŠ¢pres = substáµ’ (â‰¡áµ’-sym â„°-stmt) (ğ’«âŠ¢prog ,áµ’ ğ’«âŠ¢pres)
\end{code}

Next we turn several uses of the fixpoint theorem for ğ’±.
The successor of a value is a well-behaved â„• if-and-only-if the value
is a well-behaved â„•.

\begin{code}
ğ’±-suc : ğ’±âŸ¦ `â„• âŸ§ (`suc V) â‰¡áµ’ ğ’±âŸ¦ `â„• âŸ§ V
ğ’±-suc {V} = let X = injâ‚ (`â„• , `suc V) in
  ğ’±âŸ¦ `â„• âŸ§ (`suc V)              â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  ğ’±âŠâ„° X                         â©¦âŸ¨ fixpointáµ’ pre-ğ’±âŠâ„° X âŸ©
  â™¯ (pre-ğ’±âŠâ„° X) (ğ’±âŠâ„° , ttáµ–)     â©¦âŸ¨ â‰¡áµ’-sym (fixpointáµ’ pre-ğ’±âŠâ„° (injâ‚ (`â„• , V))) âŸ© 
  ğ’±âŸ¦ `â„• âŸ§ V                     âˆ
\end{code}

\noindent A lambda abstraction $Æ› N$ is well-behaved at type $A â‡’ B$
if-and-only-if $N [ W ]$ is well-behaved at $B$ assuming that $W$ is
well-behaved at $A$.

\begin{code}
ğ’±-fun : âˆ€{A B}{N} â†’ ğ’±âŸ¦ A â‡’ B âŸ§ (Æ› N) â‰¡áµ’ (âˆ€áµ’[ W ] ((â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))))
ğ’±-fun {A}{B}{N} = let X = (injâ‚ (A â‡’ B , Æ› N)) in
   ğ’±âŸ¦ A â‡’ B âŸ§ (Æ› N)                                         â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   ğ’±âŠâ„° X                                                    â©¦âŸ¨ fixpointáµ’ pre-ğ’±âŠâ„° X âŸ©
   â™¯ (pre-ğ’±âŠâ„° X) (ğ’±âŠâ„° , ttáµ–)                                â©¦âŸ¨ â‰¡áµ’-refl refl âŸ© 
   (âˆ€áµ’[ W ] ((â–·áµ’ (ğ’±âŸ¦ A âŸ§ W)) â†’áµ’ (â–·áµ’ (â„°âŸ¦ B âŸ§ (N [ W ])))))    âˆ
\end{code}

\noindent A fixpoint value $Î¼\,V$ is well-behaved at type $A â‡’ B$ if-and-only-if $V$ is
a value and $V[Î¼\, V]$ is well behaved at $A â‡’ B$.

\begin{code}
ğ’±-Î¼ : âˆ€{A B}{V} â†’ ğ’±âŸ¦ A â‡’ B âŸ§ (Î¼ V) â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))
ğ’±-Î¼ {A}{B}{V} = let X = (injâ‚ (A â‡’ B , Î¼ V)) in
   ğ’±âŸ¦ A â‡’ B âŸ§ (Î¼ V)                                         â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   ğ’±âŠâ„° X                                                    â©¦âŸ¨ fixpointáµ’ pre-ğ’±âŠâ„° X âŸ©
   â™¯ (pre-ğ’±âŠâ„° X) (ğ’±âŠâ„° , ttáµ–)                                â©¦âŸ¨ â‰¡áµ’-refl refl âŸ© 
   (Value V)áµ’ Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ A â‡’ B âŸ§ (V [ Î¼ V ])))               âˆ
\end{code}

If we have a well-behaved value at a function type $A â‡’ B$,
then it must either be a lambda abstraction or a fixpoint value.

\begin{code}
ğ’±-fun-case : âˆ€{ğ’«}{A}{B}{V}{R} â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A â‡’ B âŸ§ V
  â†’ (âˆ€ N â†’ V â‰¡ Æ› N â†’ ğ’« âŠ¢áµ’ R)  â†’  (âˆ€ Vâ€² â†’ V â‰¡ Î¼ Vâ€² â†’ ğ’« âŠ¢áµ’ R)  â†’  ğ’« âŠ¢áµ’ R
ğ’±-fun-case {ğ’«}{A}{B}{V}{R} âŠ¢ğ’±V contÎ» contÎ¼ =
  âŠ¢áµ’-sucP âŠ¢ğ’±V Î» { ğ’±Vsn â†’ aux{V} ğ’±Vsn contÎ» contÎ¼ }
  where aux : âˆ€{V n} â†’ # (ğ’±âŸ¦ A â‡’ B âŸ§ V) (suc n)  â†’  (âˆ€ N â†’ V â‰¡ Æ› N â†’ ğ’« âŠ¢áµ’ R)
            â†’  (âˆ€ Vâ€² â†’ V â‰¡ Î¼ Vâ€² â†’ ğ’« âŠ¢áµ’ R)  â†’  ğ’« âŠ¢áµ’ R
        aux {Æ› N} ğ’±sn contÎ» contÎ¼ = contÎ» N refl
        aux {Î¼ Vâ€²} ğ’±sn contÎ» contÎ¼ = contÎ¼ Vâ€² refl
\end{code}

\noindent A well-behaved value is of course a value.

\begin{code}
ğ’±â‡’Value : âˆ€ {k} A M â†’ # (ğ’±âŸ¦ A âŸ§ M) (suc k) â†’ Value M
ğ’±â‡’Value `â„• `zero ğ’±M = V-zero
ğ’±â‡’Value `â„• (`suc M) ğ’±M = V-suc (ğ’±â‡’Value `â„• M ğ’±M)
ğ’±â‡’Value (A â‡’ B) (Æ› N) ğ’±M = V-Æ›
ğ’±â‡’Value (A â‡’ B) (Î¼ V) (v , â–·ğ’±V[Î¼V]) = V-Î¼ v
\end{code}

\noindent A well-behaved value is also a well-behaved term.

\begin{code}
ğ’±â‡’â„° : âˆ€{A}{ğ’«}{V} â†’  ğ’« âŠ¢áµ’ ğ’±âŸ¦ A âŸ§ V  â†’  ğ’« âŠ¢áµ’ â„°âŸ¦ A âŸ§ V
ğ’±â‡’â„° {A}{ğ’«}{V} ğ’«âŠ¢ğ’±V = â„°-intro prog pres
    where prog = injâ‚áµ’ ğ’«âŠ¢ğ’±V
          pres = Î›áµ’[ N ] â†’áµ’I (pureáµ’E Záµ’ Î» Vâ€”â†’N â†’
                   âŠ¢áµ’-sucP (Sáµ’ ğ’«âŠ¢ğ’±V) Î» ğ’±V â†’
                      âŠ¥-elim (value-irreducible (ğ’±â‡’Value A V ğ’±V ) Vâ€”â†’N))
\end{code}

\subsection{Definition of Semantic Type Safety for Open Terms}
\label{sec:sem-type-safety}

The predicates ğ’± and â„° characterize well-behaved terms without any
free variables, that is, closed terms. (Note that the definition of
\textsf{pre-ğ’±} does not include a case for variables.)  However, we
shall also need to handle terms with free variables, i.e., open
terms.  The standard approach is to apply a substitution,
mapping variables to closed values, to turn the open term into a
closed term.

The following defines a well-behaved substitution, that is, a
substitution that maps variables to well-behaved values.

\begin{code}
ğ“–âŸ¦_âŸ§ : (Î“ : List Type) â†’ Subst â†’ List Setáµ’
ğ“–âŸ¦ [] âŸ§ Ïƒ = []
ğ“–âŸ¦ A âˆ· Î“ âŸ§ Ïƒ = (ğ’±âŸ¦ A âŸ§ (Ïƒ 0)) âˆ· ğ“–âŸ¦ Î“ âŸ§ (Î» x â†’ Ïƒ (suc x))
\end{code}

\noindent A term $V$ is a well-behaved open value at type $A$ in type
environment Î“ if, for any well-behaved substitution Î³, $âŸª Î³ âŸ«\, V$ is
a well behaved value.

\begin{code}
infix 3 _âŠ¨â±½_â¦‚_
_âŠ¨â±½_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
Î“ âŠ¨â±½ V â¦‚ A = âˆ€ (Î³ : Subst) â†’ ğ“–âŸ¦ Î“ âŸ§ Î³ âŠ¢áµ’ ğ’±âŸ¦ A âŸ§ (âŸª Î³ âŸ« V)
\end{code}

\noindent A term $M$ is well-behaved open term at type $A$ in
type environment Î“ if, for any well-behaved substitution Î³,
$âŸª Î³ âŸ«\, M$ is well behaved.

\begin{code}
infix 3 _âŠ¨_â¦‚_
_âŠ¨_â¦‚_ : List Type â†’ Term â†’ Type â†’ Set
Î“ âŠ¨ M â¦‚ A = âˆ€ (Î³ : Subst) â†’ ğ“–âŸ¦ Î“ âŸ§ Î³ âŠ¢áµ’ â„°âŸ¦ A âŸ§ (âŸª Î³ âŸ« M)
\end{code}

The proof of semantic type safety will make use of the Fundamental
Lemma for this logical relation, which states that a well-typed term
is also a well-behaved open term. More formally, $Î“ âŠ¢ M â¦‚ A$ implies
$Î“ âŠ¨ M â¦‚ A$ (and likewise for well-typed values).  The proof will be
by induction on the derivation of $Î“ âŠ¢ M â¦‚ A$, with a case for each
typing rule. Each of those cases will be proved in a separate
``compatibility`` lemma in Section~\ref{sec:compatibility-lemmas}.
But first we prove the ``bind'' lemma.



